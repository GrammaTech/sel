;;; csurf-asm.lisp --- Support for csurf-generated assembler files
(in-package :software-evolution-library)
(in-readtable :curry-compose-reader-macros)

(define-software csurf-asm (asm-range)
  ((assembler
    :initarg :assembler :accessor assembler :initform "nasm"
    :documentation "Assembler to use for assembling.")
   (linker
    :initarg :linker :accessor linker :initform "ld"
    :documentation "Linker to use for linking.")
   (flags
    :initarg :flags :accessor flags
    :initform
    '("-m" #+x86-64 "elf_x86_64" #-x86-64 "elf_x86" "-e" "_start" "-lc")
    :documentation "Flags to use for linking")
   (asm-flags
    :initarg :asm-flags :accessor asm-flags
    :initform '("-f" #+x86-64 "elf64" #-x86-64 "elf32" "-Ox")
    :documentation "Flags to pass to assembler.")
   (redirect-file
    :initarg :redirect-file :accessor redirect-file :initform nil
    :documentation "CodeSurfer redirect file to redirect elf copy relocations.")
   (weak-symbols
    :initarg :weak-symbols :accessor weak-symbols :initform nil
    :copier :direct
    :documentation "Symbols to weaken with `elf-weaken-gmon-start'."))
  (:documentation "Software object for ASM generated by CodeSurfer."))

(defvar *dynamic-linker-path*
  ;; Find the dynamic linker by pulling it from an executable on the system.
  #+unix (->> (split-sequence #\Newline
                (shell "ldd ~a" (trim-whitespace (shell "which ls"))))
              (mappend {split-sequence #\space})
              (mapcar #'trim-whitespace)
              (find-if {search "ld-linux"} ))
  #-unix (error "No analog for dynamic linker when not on Linux.")
  "Path to the dynamic linker on this system.")

(defvar *elf-copy-redirect-path* "elf_copy_redirect"
  "Path to elf_copy_redirect (or just the name if it's on the path).")

(defvar *elf-edit-symtab-path*
  #+x86-64 "elf_edit_symtab64"
  #+(or i386 i686) "elf_edit_symtab32"
  #+(not (or x86-64 i386 i686))
  (warn "Unable to initialize `*elf-edit-symtab-path*'.")
  "Path to elf_edit_symtab64 or 32 (or just the name if it's on the path).")

(defun elf-weaken-gmon-start (elf-objfile symbols)
  "Run elf_edit_symtab on ELF-OBJFILE and each symbol in SYMBOLS.
Reimplementation of CSURF elf:weaken-gmon-start.
* ELF-OBJFILE - an object file
* SYMBOLS - a list of strings representing symbols to mark as weakly required
Return a list of pairs whose first element is a symbol and whose second element
is the error number after running `*elf-edit-symtab-path*' on that symbol.
"
  (let ((cmd-path (namestring *elf-edit-symtab-path*)))
    (iter (for sym in symbols)
          (multiple-value-bind (stdout stderr errno)
              (shell "~a ~a ~a 2" cmd-path elf-objfile sym)
            (declare (ignorable stdout stderr))
            (unless (zerop errno)
              (collect (cons sym errno)))))))

(defun elf-copy-redirect (elf-file redirect-file)
  "Reimplementation of CSURF elf:copy-redirect.
Redirect ELF COPY relocations and associated symbols, for entries
described in redirect-file.  Requires GT_HOME environment variable to be
set."
  (shell "~a -v -s ~a ~a"
         (namestring *elf-copy-redirect-path*)
         redirect-file elf-file))

(defmethod phenome ((asm csurf-asm) &key (bin (temp-file-name)))
  "Assemble and link ASM into binary BIN.
1. Run `assembler' with `asm-flags'. If unsuccessful, return early.
2. If ASM contains `weak-symbols', mark them as weakly required
   (see `elf-weaken-gmon-start').
3. Run `linker' with `flags'.
4. If `redirect-file' is specified, run `elf-copy-redirect'."
  ;; In CSURF-generated asm, mark some symbols, e.g.  __gmon_start__,
  ;; as weakly required.  The first value returned will be the name of
  ;; the binary on success, but may be the name of the object file if
  ;; there is a failure prior to linking.
  (with-temp-file-of (src "s") (genome-string asm)
    (with-temp-file (obj)
      ;; Assemble.
      (multiple-value-bind (stdout stderr errno)
          (shell "~a -o ~a ~a ~{~a~^ ~}"
                 (assembler asm) obj src (asm-flags asm))
        (if (not (zerop errno))
            (values obj errno stderr stdout src)
            ;; Mark __gmon_start__ et al. as weakly required.
            (if (elf-weaken-gmon-start obj (weak-symbols asm))
                ;; Errors in `elf-weaken-gmon-start'.
                (values obj (max errno 1) stderr stdout src)
                ;; Link.
                (multiple-value-bind (stdout stderr errno)
                    (shell "~a -o ~a ~a ~{~a~^ ~}"
                           (or (linker asm) *asm-linker*)
                           bin obj
                           (append (flags asm) (list "--dynamic-linker"
                                                     *dynamic-linker-path*)))
                  (if (or (not (zerop errno)) (not (redirect-file asm)))
                      ;; Errors linking.
                      (values bin (max errno 1) stderr stdout src)
                      ;; Elf copy-redirect.
                      (multiple-value-bind (stdout stderr errno)
                          (elf-copy-redirect bin (redirect-file asm))
                        (values bin errno stderr stdout src))))))))))


;; Parsing csurf output (for asm/linker flags, weak symbols, redirects)
(defun parse-and-apply-command (csurf-asm bracket-cmd)
  "Parse BRACKET-CMD to update fields of CSURF-ASM software object.
Currently only populates the `weak-symbols' field from the log."
  ;; Trim brackets.
  (flet ((is-program-cmd (program cmd)
           ;; Check if the command CMD is invoking program PROGRAM.
           ;; CMD is a list of strings representing a command. Check
           ;; if the first element ends with the string PROGRAM (to
           ;; allow for qualified paths).
           (and program (ends-with-subseq program (first cmd) :test #'equal)))
         #+(or )                        ; NOTE: Unused
         (extract-linker-flags
             (cmd &aux (linker-ignore-flags '("-o" ">" "1>" "2>" "&>")))
           ;; Return a list of the flags for a linker command from
           ;; CMD.  CMD is a list of strings representing a command.
           ;; Remove the first element which is the program name.
           ;; Remove any ignored flags and their parameters, each is
           ;; assumed to have exactly one.  Remove any options ending
           ;; with \".o\", assuming that there is only one and it's
           ;; the file for the software object and so shouldn't be
           ;; linked again.
           ;; 
           ;; Use cdr to remove linker program name.
           (iter (for str in (cdr cmd))
                 (for i upfrom 0)
                 (with next-removal = -1)
                 ;; For strings that match a flag/redirect exactly,
                 ;; remove the following element (the argument to the
                 ;; flag).
                 (when (member str linker-ignore-flags :test #'equal)
                   (setf next-removal (1+ i)))
                 ;; Keep only if the element isn't flagged for
                 ;; removal, doesn't start with one of the ignore
                 ;; flags/redirects, and isn't a .o file.
                 (unless
                     (or (= i next-removal)
                         (some {starts-with-subseq _ str} linker-ignore-flags)
                         (ends-with-subseq ".o" str :test #'equal))
                   (collect str into extract-flags))
                 (finally (return extract-flags)))))
    (let ((cmd (->> (subseq bracket-cmd 1 (1- (length bracket-cmd)))
                    (split "\\s+"))))
      (cond
        ;; NOTE: Disabled as the defaults above are generally better for now.
        ;; Assembler command.
        ;; ((is-program-cmd (assembler csurf-asm) cmd)
        ;;  (setf (asm-flags csurf-asm)
        ;;        ;; Remove first argument (program name) and last (file name).
        ;;        (butlast (cdr cmd))))
        ;; Mark symbols weakly required.
        ((is-program-cmd *elf-edit-symtab-path* cmd)
         (setf (weak-symbols csurf-asm)
               ;; Symbol is the second to last argument in elf_edit_symtab.
               (adjoin (lastcar (butlast cmd))
                       (weak-symbols csurf-asm)
                       :test #'equal)))
        ;; NOTE: Disabled as the defaults above are generally better for now.
        ;; Linker cmd.
        ;; ((is-program-cmd (linker csurf-asm) cmd)
        ;;  (setf (flags csurf-asm)
        ;;        (extract-linker-flags cmd)))
        ;; Elf copy redirect file.
        ;; ((is-program-cmd *elf-copy-redirect-path* cmd)
        ;;  (setf (redirect-file csurf-asm)
        ;;        (lastcar (butlast cmd))))
        ;; Some other command.
        (t nil)))))

(defmethod apply-config ((obj csurf-asm) log-file)
  "Parse LOG-FILE to update fields of CSURF-ASM software object."
  (when (probe-file log-file)
    (with-open-file (in (probe-file log-file))
      (iter (for line = (ignore-errors (read-line in)))
            (while line)
            (multiple-value-bind (is-cmd bracket-cmd)
                (starts-with-subseq "#[subr system]" line :return-suffix t)
              (when is-cmd
                (parse-and-apply-command obj bracket-cmd))))))
  obj)
