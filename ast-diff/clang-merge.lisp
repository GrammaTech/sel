;;;; clang-merge.lsip -- command line interface for three way merging
(defpackage :software-evolution-library/ast-diff/clang-merge
  (:nicknames :sel/ast-diff/clang-merge)
  (:documentation "Command line interface for three way merging of software")
  (:use :common-lisp
	:alexandria
        :command-line-arguments
        :metabang-bind
        :arrow-macros
        :named-readtables
        :curry-compose-reader-macros
        :iterate
	:split-sequence
	:uiop
        :bordeaux-threads
        :uiop/filesystem
        :software-evolution-library/utility
        :software-evolution-library/components/formatting
	:software-evolution-library/ast-diff/ast-diff
        :software-evolution-library
        :software-evolution-library/software/project
        :software-evolution-library/software/clang
        :software-evolution-library/software/clang-project)
  (:shadowing-import-from
   :uiop :getenv :directory-exists-p :copy-file :appendf :parse-body
   :ensure-list :simple-style-warning :ensure-gethash :ensure-function
   :if-let :emptyp :featurep :truenamize)
  (:export :clang-merge :run-clang-merge))
(in-package :sel/ast-diff/clang-merge)
(in-readtable :curry-compose-reader-macros)

(defun handle-set-quiet-argument (arg)
  (declare (ignorable arg))
  (setf *note-level* 0))

(defun handle-set-verbose-argument (level)
  (when (>= level 4) (setf *shell-debug* t))
  (setf *note-level* level))

(defun handle-comma-delimited-argument (argument)
  (split-sequence #\, argument :remove-empty-subseqs t))

(defun read-compilation-database (file)
  "Read a Clang compilation database from FILE.

* FILE holds a JSON compilation database as generated by the bear utility."
       (with-open-file (in file :direction :input)
         (remove-duplicates (json:decode-json-from-source in)
                            :test #'equalp :key {aget :file} :from-end t)))

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defparameter +command-line-options+
    '((("help" #\h #\?) :type boolean :optional t
       :documentation "display help output")
      (("build-command" #\b) :type string :optional t :initial-value "make"
       :documentation "shell command to build project directory")
      (("compiler" #\c) :type string :initial-value "clang"
       :documentation "use CC as the C compiler")
      (("flags" #\F) :type string :optional t
       :action #'handle-comma-delimited-argument
       :documentation "comma-separated list of compiler flags")
      (("compilation-database" #\C) :type string :optional t
       :action #'read-compilation-database
       :documentation "path to clang compilation database")
      (("quiet" #\q) :type boolean :optional t
       :action #'handle-set-quiet-argument
       :documentation "set verbosity level to 0")
      (("verbose" #\V) :type integer :optional t :initial-value 2
       :action #'handle-set-verbose-argument
       :documentation "verbosity level 0-4"))))

(define-command clang-merge (original version1 version2 out-dir
                                      &spec +command-line-options+
                                      &aux original-soft version1-soft version2-soft
                                      project-name)
  "Merge VERSION1 and VERSION2 sharing ancestor BASE into OUT-DIR."
  #.(format nil
            "~%Built from SEL ~a, and ~a ~a.~%"
            +software-evolution-library-version+
            (lisp-implementation-type) (lisp-implementation-version))
  (declare (ignorable quiet verbose compiler))
  (when help (show-help-for-clang-merge))
  (mapc (lambda (file)
	  (unless (probe-file file)
	    (format *error-output*
		    "~a: No such file or directory~%"
		    file)
	    (return-from clang-merge 3))
	  file) (list original version1 version2))
  (setf out-dir (pathname (concatenate 'string out-dir "/")))
  (ensure-directories-exist out-dir)
  (setf out-dir (pathname-directory out-dir))

  ;; These functions were defined in BI
  ;; They should get moved to SEL as global
  ;; utility functions.  For now, use FLET
  (flet ((resolve-out-dir-from-source (source)
	   (if-let ((as-dir (directory-p source)))
	     (butlast (pathname-directory as-dir))
	     (pathname-directory source)))
	 (resolve-name-from-source (source)
	   (if-let ((as-dir (directory-p source)))
	     (lastcar (pathname-directory as-dir))
	     (pathname-name source)))
         #+unused-function
	 (resolve-store-path-from-out-dir-and-name (out-dir name)
	   (namestring (make-pathname :directory out-dir
				      :name name
				      :type "store"))))
    (setf original (truenamize original)
	  version1 (truenamize version1)
	  version2 (truenamize version2)
	  out-dir (or out-dir (resolve-out-dir-from-source original))
	  project-name (resolve-name-from-source original)))

  (note 1 "Parameters:~%~S~%"
        `((original . ,original)
          (version1 . ,version1)
          (version2 . ,version2)
          (out-dir . ,out-dir)
          (*note-level* . ,*note-level*)))

  (note 3 "Creating software objects")
  (flet ((%create (s)
	   (from-file (apply
                       #'make-instance
                       'clang-project
                       :project-dir s
                       (append
                        (when flags
                          (list :flags flags))
                        (when compilation-database
                          (list :compilation-database compilation-database))
                        (when build-command
                          (list :build-command build-command))))
                      s)))
    (setf original-soft (%create original))
    (setf version1-soft (%create version1))
    (setf version2-soft (%create version2)))

  ;; Create styled versions of the input files
  (save-to original-soft out-dir "original")
  (save-to version1-soft out-dir "v1")
  (save-to version2-soft out-dir "v2")

  ;; Perform merge
  (note 3 "Performing merge")
  (multiple-value-bind (new-merged unstable)
      (converge original-soft version1-soft version2-soft)
    (save-to new-merged out-dir "merged")
    (if (not unstable)
	(note 1 "No merge conflicts")
	(note 0 "Merge conflicts:~%~a~%" unstable))
    ;; exit with 0 if no merge conflicts, 1 otherwise
    (return-from clang-merge (if unstable 1 0))))

(defgeneric save-to (soft out-dir sub))

(defmethod save-to ((soft project) out-dir sub)
  (let ((dest (make-pathname :directory (append out-dir (list sub)))))
    (unless (probe-file dest)
      (to-file (copy soft) dest))))
